#!/usr/bin/env python
#         --Common SConscripts--
# Things below here will be shared between Kroll and
# any parent builds (ie Titanium) -- so basically no
# Kroll-specific stuff below here
import os.path as path
Import('build')
Import('debug')
Import('thirdparty_check')

SConscript('boot/SConscript', variant_dir=path.join(build.dir, 'objs', 'boot'), duplicate=0)

build.add_thirdparty(build.env, 'boost')
build.add_thirdparty(build.env, 'poco')

#TODO: boost 1.46 will have default filesystem version 3... right now its not that stable.
#we have to migrate once its stable.
build.env.Append(CPPDEFINES = ('BOOST_FILESYSTEM_VERSION', 2))

SConscript('libkroll/SConscript', variant_dir=path.join(build.dir,'objs','libkroll'), duplicate=0)

# Now that libkroll is built add it as a default for
# all the following build steps. This means that things
# that should not depend on libkroll should be built
# before here.
build.env.Append(CPPPATH=[path.join(build.kroll_source_dir, 'libkroll')])
build.env.Append(LIBS=['khost'])
build.env.Append(LIBPATH=[path.join(build.dir, 'runtime', build.version)])
