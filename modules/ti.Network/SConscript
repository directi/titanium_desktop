#!/usr/bin/env python
from os import path
Import('build env module')

sources = Glob('*.cpp') + Glob('protocols/irc/*.cpp') + \
	Glob('protocols/http/*.cpp') + Glob('proxy/*.cpp') + Glob('protocols/tcp/*.cpp')

env.Append(CPPDEFINES = ('TITANIUM_NETWORK_API_EXPORT', 1))

if build.is_win32():
	sources += Glob('win32/*.cpp')
elif build.is_linux():
	sources += Glob('linux/*.cpp')
elif build.is_osx():
	sources += Glob('osx/*.cpp')

if build.is_osx():
	env.Append(FRAMEWORKS=['SystemConfiguration'])

if build.is_linux():
	env.ParseConfig('pkg-config --libs --cflags gdk-2.0 dbus-glib-1')

if build.is_win32():
	build.add_thirdparty(env, 'openssl')
	build.add_thirdparty(env, 'curl')
	env.Append(LIBS=['wsock32', 'ws2_32', 'ole32', 'comsuppw', 'wbemuuid'])

# Copy the support files the module build directory
build.mark_build_target(build.utils.CopyTree(Glob('support/*'), module.dir))
build.mark_build_target(env.SharedLibrary(
	path.join(module.dir, 'tinetworkmodule'), sources))
