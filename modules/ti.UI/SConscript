#!/usr/bin/env python
from os import path
Import('build env module')

env.Append(LIBPATH=[build.get_module('tiapp').dir])
env.Append(CPPPATH = ['%s/modules/ti.App' % build.titanium_source_dir])

build.add_thirdparty(env, 'webkit')
env.Append(LIBS=['tiappmodule'])

sources = Glob('*.cpp') + ['url/url.cpp']
if build.is_linux():
	sources = sources + Glob('gtk/*.cpp')
	env.Append(LIBS=['Xss', 'libsoup-2.4', 'libsoup-gnome-2.4'])
	env.ParseConfig('pkg-config --cflags --libs libnotify gtk+-2.0 gdk-2.0 x11 xext')

elif build.is_win32():
	build.add_thirdparty(env, 'libpng')
	build.add_thirdparty(env, 'icu')
	sources = sources + Glob('win32/*.cpp')
	sources += Glob('binding/kjs*.cpp')
	env.Append(LIBS=['shell32', 'comdlg32', 'comctl32',
		'gdi32', 'comsuppw'])

elif build.is_osx():
	env.Append(FRAMEWORKPATH=[build.tp('growl')])
	env.Append(FRAMEWORKS=['Cocoa','Carbon', 'IOKit', 'Growl'])
	sources = sources + Glob('osx/*.mm') + Glob('osx/*.cpp')
	env.Append(CCFLAGS=['-x', 'objective-c++'])

# Copy the support files the module build directory
build.mark_build_target(build.utils.CopyTree(Glob('support/*'), module.dir))

build.mark_build_target(env.SharedLibrary(
	path.join(module.dir,'tiuimodule'), sources))
